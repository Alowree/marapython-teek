---
date: 2025-06-25 09:20:52
title: 数字英文转换
permalink: /pages/1b3b85
categories:
  - Editor
  - Excel
tags:
  - Excel
  - VBA
---

严格意义上来讲，Excel 当然不能归为编辑器这一类工具，但是考虑到外贸从业人士会经常使用 Excel 中的表格自动计算功能，来制作产品报价单或商业发票等文件，这基本上是将 Excel 当做 Word 使用，就姑且将之归类到编辑器吧。

<!-- more -->

本文主要记录，通过 Excel 中内置的 VBA 实现从数字到英文的转换，也就是把使用数字表示的金额转化成使用英语表述的金额，主要适用于商业发票的制作。有了这个小脚本之后，就不再需要每次都是人工去将每张发票中的金额转述成英文表达了，每次可以节省大约一分钟，并且不会出错。

## 步骤

1. 在标准的 Excel 文件内，通过快捷键 `Alt + F11` 打开 VBA 编辑器
2. 插入一个新模块，默认名称 Module1
3. 将 VBA 脚本复制粘贴至 Module1
4. 将标准的 `Excel.xlsx` 文件另存为包含宏的文件格式，即以 `.xlsm` 为后缀

## 使用

在保存了脚本的 Excel 文件内的某个单元格使用自动转换功能时，只需简单地调用宏脚本内定义好的函数 `=AmountToWords(K33, "US Dollars", "Cents")` 即可，可以传入 3 个参数：

- 引用数字的位置（或者地址）
- 货币名称（比如 US Dollars，HK Dollars）
- 最小精度单位（一般就是小数点后的第二位，即「分」）

## 脚本

```
Option Explicit
' VBA Function to Convert Number to Word in Excel
Function NumToWords(ByVal MyNumber, Optional isProper As Boolean = False) As String
    Dim Units As String, SubUnits As String, TempStr As String
    Dim DecimalPlace As Integer, Count As Integer
    Dim Place(9) As String

    Place(2) = " Thousand "
    Place(3) = " Million "
    Place(4) = " Billion "
    Place(5) = " Trillion "

    ' Check if the input is empty or not a number
    If IsEmpty(MyNumber) Or Not IsNumeric(MyNumber) Then
        NumToWords = ""
        Exit Function
    End If

    ' Convert MyNumber to string and trim white space
    MyNumber = Trim(Str(MyNumber))

    ' Handle Zero Case
    If Val(MyNumber) = 0 Then
        NumToWords = "Zero"
        Exit Function
    End If

    ' Find position of decimal place (0 if none)
    DecimalPlace = InStr(MyNumber, ".")

    ' Convert Decimal Part
    If DecimalPlace > 0 Then
        SubUnits = GetDecimalWords(Mid(MyNumber, DecimalPlace + 1), isProper)
        MyNumber = Trim(Left(MyNumber, DecimalPlace - 1))
    End If

    ' Process Integer Part
    Count = 1
    Do While MyNumber <> ""
        TempStr = GetHundreds(Right(MyNumber, 3))
        If TempStr <> "" Then Units = TempStr & Place(Count) & Units
        If Len(MyNumber) > 3 Then
            MyNumber = Left(MyNumber, Len(MyNumber) - 3)
        Else
            MyNumber = ""
        End If
        Count = Count + 1
    Loop

    ' Combine Integer and Decimal Parts
    If SubUnits = "" Then
        NumToWords = Application.Trim(Units)
    Else
        NumToWords = Application.Trim(Units & " Point " & SubUnits)
    End If
End Function

' Converts a number from 100-999 into text
Function GetHundreds(ByVal MyNumber)
    Dim Result As String
    If Val(MyNumber) = 0 Then Exit Function
    MyNumber = Right("000" & MyNumber, 3)

    ' Convert Hundreds Place
    If Mid(MyNumber, 1, 1) <> "0" Then
        Result = GetDigit(Mid(MyNumber, 1, 1)) & " Hundred "
        ' Add "and" if there are tens or ones
        If Mid(MyNumber, 2, 2) <> "00" Then
            Result = Result & " and "
        Else
            Result = Result & " "
        End If
    End If

    ' Convert Tens and Ones Place
    If Mid(MyNumber, 2, 1) <> "0" Then
        Result = Result & GetTens(Mid(MyNumber, 2))
    Else
        Result = Result & GetDigit(Mid(MyNumber, 3))
    End If

    GetHundreds = Result
End Function

' Converts a number from 10 to 99 into text
Function GetTens(TensText)
    Dim Result As String
    Result = ""

    If Val(Left(TensText, 1)) = 1 Then ' If value between 10-19...
        Select Case Val(TensText)
            Case 10: Result = "Ten"
            Case 11: Result = "Eleven"
            Case 12: Result = "Twelve"
            Case 13: Result = "Thirteen"
            Case 14: Result = "Fourteen"
            Case 15: Result = "Fifteen"
            Case 16: Result = "Sixteen"
            Case 17: Result = "Seventeen"
            Case 18: Result = "Eighteen"
            Case 19: Result = "Nineteen"
        End Select
    Else ' If value between 20-99...
        Select Case Val(Left(TensText, 1))
            Case 2: Result = "Twenty"
            Case 3: Result = "Thirty"
            Case 4: Result = "Forty"
            Case 5: Result = "Fifty"
            Case 6: Result = "Sixty"
            Case 7: Result = "Seventy"
            Case 8: Result = "Eighty"
            Case 9: Result = "Ninety"
        End Select
        ' Add hyphen between tens and ones
        If Right(TensText, 1) <> "0" Then
            Result = Result & "-" & GetDigit(Right(TensText, 1))
        End If
    End If

    GetTens = Result
End Function

' Converts a number from 1 to 9 into text
Function GetDigit(Digit)
    Select Case Val(Digit)
        Case 1: GetDigit = "One"
        Case 2: GetDigit = "Two"
        Case 3: GetDigit = "Three"
        Case 4: GetDigit = "Four"
        Case 5: GetDigit = "Five"
        Case 6: GetDigit = "Six"
        Case 7: GetDigit = "Seven"
        Case 8: GetDigit = "Eight"
        Case 9: GetDigit = "Nine"
        Case Else: GetDigit = ""
    End Select
End Function

' Converts Decimal Digits to Words
Function GetDecimalWords(DecimalPart As String, isProper As Boolean) As String
    Dim i As Integer, DecWord As String

    If isProper Then
        ' Proper: 25.57 ? "Twenty Five Point Five Seven"
        For i = 1 To Len(DecimalPart)
            If i > 1 Then DecWord = DecWord & " "
            DecWord = DecWord & GetDigit(Mid(DecimalPart, i, 1))
        Next i
    Else
        ' Default: 25.57 ? "Twenty Five Point Fifty Seven"
        DecWord = GetTens(Left(DecimalPart & "00", 2))
    End If

    GetDecimalWords = DecWord
End Function


Function AmountToWords(ByVal MyNumber, ByVal strCurrency As String, ByVal strUnits As String) As String
    Dim IntegerPart As String, DecimalPart As String
    Dim DecimalPlace As Integer

    ' Check if the input is empty or not a number
    If IsEmpty(MyNumber) Or Not IsNumeric(MyNumber) Then
        AmountToWords = ""
        Exit Function
    End If

    ' Convert MyNumber to string and trim white space
    MyNumber = Trim(Str(MyNumber))

    ' Find position of decimal place (0 if none)
    DecimalPlace = InStr(MyNumber, ".")

    ' Get Integer and Decimal Parts
    If DecimalPlace > 0 Then
        IntegerPart = Trim(Left(MyNumber, DecimalPlace - 1))
        DecimalPart = Right(MyNumber, Len(MyNumber) - DecimalPlace)
    Else
        IntegerPart = MyNumber
        DecimalPart = ""
    End If

    ' Initialize with "Total Say "
    AmountToWords = "Total Say "

    ' Convert Integer Part to words if not empty or zero
    If IntegerPart <> "" And Val(IntegerPart) > 0 Then
        AmountToWords = AmountToWords & NumToWords(IntegerPart) & " " & strCurrency
    End If

    ' Convert Decimal Part to words if not empty or zero
    If DecimalPart <> "" And Val(DecimalPart) > 0 Then
        If IntegerPart = "" Or Val(IntegerPart) = 0 Then
            AmountToWords = AmountToWords & NumToWords(Left(DecimalPart & "00", 2)) & " " & strUnits
        Else
            AmountToWords = AmountToWords & " and " & NumToWords(Left(DecimalPart & "00", 2)) & " " & strUnits
        End If
    End If
End Function
```

## 重构

由于 VBA 很少使用，上述实现是否可以通过 Excel + Python 重构出来？

2025-12-18 更新

发现 Mac 当前安装的 Excel 已经支持使用 Python，具体位置通过菜单 `Excel → Formulas → Insert Python` 可以到达。

将下面代码复制粘贴至需要的单元格内，`=py()`：

```python
def to_words(n):
    units = ["", "One", "Two", "Three", "Four", "Five", "Six", "Seven", "Eight", "Nine"]
    teens = ["Ten", "Eleven", "Twelve", "Thirteen", "Fourteen", "Fifteen", "Sixteen", "Seventeen", "Eighteen", "Nineteen"]
    tens = ["", "", "Twenty", "Thirty", "Forty", "Fifty", "Sixty", "Seventy", "Eighty", "Ninety"]
    thousands = ["", "Thousand", "Million", "Billion", "Trillion"]

    def convert_chunk(num):
        res = ""
        h, t = divmod(num, 100)
        if h:
            res += units[h] + " Hundred "
            if t > 0: res += "and "

        if 10 <= t < 20:
            res += teens[t-10]
        else:
            t_digit, o_digit = divmod(t, 10)
            if t_digit:
                res += tens[t_digit]
                if o_digit: res += "-"
            if o_digit:
                res += units[o_digit]
        return res.strip()

    if n == 0: return "Zero"

    res = ""
    for i in range(len(thousands)):
        n, chunk = divmod(n, 1000)
        if chunk:
            res = convert_chunk(chunk) + " " + thousands[i] + " " + res
    return res.strip()

def amount_to_words(number, currency="US Dollars", units="Cents"):
    if number is None: return ""

    # Split integer and decimal
    integer_part = int(number)
    decimal_part = int(round((number - integer_part) * 100))

    # Build String
    words = f"Total Say {to_words(integer_part)} {currency}"

    if decimal_part > 0:
        words += f" and {to_words(decimal_part)} {units}"

    return words

# Change "K33" to your actual cell reference
val = xl("K33")
amount_to_words(val)
```

## 总结

- 抛弃过气的 VBA 宏，文件可以 `.slsx` 保存和分发，收件者不会再收到安全报警
- 可扩展以处理更大数字单位
- 数字转换和币制单位有了逻辑隔离
